Thm BoolTest : [ bool -> bool true ] by [
  { lam x. if x then 'tt else 'ff };
  auto
].

Thm PathTest : [ paths({_}. S1; base; base) true ] by [
  { <x> 'loop{x} }; auto
].

Thm FunElimTest : [ (bool -> bool) -> bool true ] by [
  { lam f. let x = f {'tt}. hyp{x} };

  auto
].

Thm S1ElimTest : [ S1 -> S1 true ] by [
  { lam s.
    case s of
       base => 'base
     | loop{x} => 'loop{x}
  };

  auto
].

// Useful for stepping through a proof RedPRL completes automatically, to see
// what is being done.
Tac TryStep = [ auto-step || id ].

Thm ApEqTest : [
  (f : bool -> bool)
    -> paths({_}. bool; f tt; f tt) true
] by [
  { lam f. <_> '(,f tt) };

  // Try commenting out the following line, and stepping through the proof with tryStep().
  auto
].

Def BoolEta(#M : exp) : exp = [
  if([a].bool; #M; tt; ff)
].

// Let's prove the existence of a path between the identity function on booleans, and the
// function that takes a boolean to a vacuous if statement.
//
// The most fun way to use RedPRL is interactively! We will write our proof in the development
// calculus. The advantage of this style of proof is that we can leave holes, and interactively figure out
// what we need to do.
//
Thm PathTest2 : [ paths({_}. bool -> bool; lam([b].b); lam([b].BoolEta(b))) true ] by [
  { let h : [(b:bool) -> paths({_}.bool; b; BoolEta(b)) true] =
      lam b. if b then <y> 'tt else <y> 'ff.
    <x> lam b. let p = h hyp{b}. '(,p @ x)
  };

  auto
].

// It turns out that it is just as good to figure out what the witness program for this path is
// by hand, and then tell RedPRL to try and prove that it does in fact witness this path.
// This approach has the advantage of being far more concise, but it has the disadvantage of
// not being interactive: you must know ahead of time the entirety of the program, and
// cannot take advantage of types in order to synthesize part of it.
Thm PathTest3 : [ paths({_}. bool -> bool; lam([b].b); lam([b].BoolEta(b))) true ] by [
  // I'm surprised that RedPRL can typecheck this properly! Quite encouraging.
  '(<x> lam([b]. if([b].paths({_}.bool; b; BoolEta(b)); b; <_> tt; <_> ff) @ x));
  auto
].

Thm PairTest : [ (a : S1) * paths({_}. S1; a; base) true ] by [
  < {'base}
  , <x> 'loop{x}
  >;
  auto
].


Def Not : exp = [
  lam([x]. if/s(x; ff; tt))
].

Def Cmp(#f : exp; #g : exp) : exp = [
  lam([x]. #f (#g x))
].

Thm StrictBoolTest : [ Not() = Cmp(Not(); Cmp(Not(); Not())) in sbool -> sbool ] by [
  auto
].
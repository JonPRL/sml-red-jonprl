// Some theorems from http://www.nuprl.org/MathLibrary/LogicalInvestigations/.

// We will prove the theorem A -> B -> A. In a traditional proof assistant for type theory, the 
// types/propositions A, B would be quantified over using dependent function types and a universe.
// However, RedPRL doesn't yet have a universe! So, what we have to do is treat A and B as parameters
// to the theorem, and add explicit *hypotheses* that they are types.
//
// In RedPRL, parameters to definitions and theorems are prefixed by a #; this distinguishes them from 
// object variables.
//
// Once we have a universe, this will be much simpler.
Thm Thm1(#A; #B) : [
  type/A : #A type,
  type/B : #B type
  >> (-> #A #B #A)
] by [
  lam a, b. hyp a
].



// We'll make a handy little tactic to apply a functional hypothesis.
Tac Apply{z:hyp}(#t:tac) = [
  // RedPRL's logic is a sequent calculus, which means that you apply 
  // functions in your context using let bindings in the following way:
  let x = z #t.
  hyp x
].


// #{t} tells the parser that 't' should be parsed using tactic notation.

Thm Thm2(#A; #B; #C) : [
  type/A : #A type,
  type/B : #B type,
  type/C : #C type
  >> (-> (-> #A #B) (-> #A #B #C) #A #C)
] by [
  lam f, g, a.
  let h = g {hyp a}.
  ({Apply h}
   ({Apply f}
    #{hyp a}))
].

// It is worthwhile to print out the extract program / evidence for Thm2.
Extract Thm2.

// here's a proof using lower-level scripting
Thm Thm3/low-level(#A; #B; #C) : [
  type/A : #A type,
  type/B : #B type,
  type/C : #C type
  >> (->
      (-> #A #B)
      (-> #B #C)
      (-> #A #C))
] by [
  fresh ab, bc, a <- auto;
  fresh c <- elim bc;
  [fresh b <- elim ab; [hyp a, hyp b], hyp c]
].

Extract Thm3/low-level.

// here's a high-level version of the above proof. proofs using the high-level 
// programming calculus may be longer, but they are often easier to engineer, 
// and nicely segregate main goals from auxiliary goals.
Thm Thm3/high-level(#A; #B; #C) : [
  type/A : #A type,
  type/B : #B type,
  type/C : #C type
  >> (->
      (-> #A #B)
      (-> #B #C)
      (-> #A #C))
] by [
  lam ab, bc, a.
  ({Apply bc}
   ({Apply ab}
    #{hyp a}))
].

Extract Thm3/high-level.

Def Not(#A) = [ (-> #A void) ].

Thm Not/wf(#A) : [
  type/A : #A type
  >> (Not #A) type
] by [
  auto
].


Thm Thm4(#A; #B) : [
  type/A : #A type
  >> (-> (Not #A) #A #B)
] by [
  lam r, a.
  unfold Not;
  let boom = r {hyp a}.
  elim boom
].

Thm Thm5(#A) : [
  type/A : #A type
  >> (-> #A (Not (Not #A)))
] by [
  lam a. unfold Not; lam r.
  ({Apply r}
   #{hyp a})
].

Print Thm4.
Print Thm5.


Thm Thm6(#A;#B) : [
  type/A : #A type,
  type/B : #B type
  >> (-> (-> #A #B) (Not #B) (Not #A))
] by [
  lam ab, nb. unfold Not; lam a.
  ({Apply nb} 
   ({Apply ab}
    #{hyp a}))
].
Tac GetHole(#c : [exp].exp, #t : [exp].tac) = [
  query gl <- goal.
  match [gl:jdg] {
    [hole | #jdg{(#c %hole)} => (#t %hole)]
  }
].

// We can write a cool user-defined tactic for asserting and then rewriting along an equality.
// (Rewrite n a [x] c) matches the goal against the motive "[x] c" and rewrites along
// the equality [_ = n in a].
Tac Rewrite(#c : [exp].exp, #n, #a, #t : tac) = [
  (GetHole [x] (#c x) [hole] #tac{
    let p : [hole = #n in #a] = #t.
    // Use the elimination rule for equality. We bind a new hypothesis which will represent the location
    // in the goal #c which is being rewritten.
    fresh x -> rewrite-hyp p;
    [`(#c x), id, auto, auto]
  })
].

Thm EqualityElimTest : [
  (-> [b : bool] (path [_] bool tt (if tt tt ff)))
] by [
  // We're going to prove this in a silly way to illustrate equality elimination.
  // We'll rewrite the goal by asserting [(if tt tt ff) = tt in bool].
  (Rewrite 
    [x] (-> bool (path [_] bool tt x))
    tt bool #tac{auto});
  // observe that the goal has now been rewritten!
  ?check-this-out;
  lam b. <_> `tt
].

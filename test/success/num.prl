Thm One : [
  1 in int
] by [
  auto
].

Thm NegOne : [
  -1 in int
] by [
  auto
].

Thm NatOne : [
  1 in nat
] by [
  auto
].

Thm NatIsInt : [
  (-> [x : nat] (= int x x))
] by [
  lam x => fresh x', x'/ih -> elim x; auto
].

Thm Pred : [
  (-> nat nat)
] by [
  lam a =>
  fresh a', ind -> elim a;
  [ `zero ];
  [ `a' ]
].

// A tactic to infer and supply a simple (non-dependent) motive to the nat/eq/nat-rec rule.
Tac SimpleNatRecEq = [
  query gl <- concl in
  match gl {
    [n,z,s,ty | #jdg{(nat-rec %n %z [x y] (%s x y)) in %ty} =>
      refine nat/eq/nat-rec; [`%ty]; auto]
  }
].

Print SimpleNatRecEq.

Thm Plus : [
  (-> nat nat nat)
] by [
  lam a =>
  fresh a', ind -> elim a;
  [ lam x => use x
  , lam x => let ih/x = ind [use x] in `(succ ih/x)
  ]
].

Thm Plus/wf : [
  Plus in (-> nat nat nat)
] by [
  auto
].

Thm Plus/zeroL : [
  (-> [n : nat] (= nat ($ Plus 0 n) n))
] by [
  lam n => auto
].

Thm Plus/zero/R : [
  (-> [n : nat] (= nat ($ Plus n 0) n))
] by [
  lam n =>
    fresh n', ind -> elim n;
    [ `ax
    , fresh x -> rewrite-hyp ind;
      [ `(= nat (succ x) (succ n'))
      , auto
      , auto
      , auto; refine fun/eq/app; [`(-> nat nat)]; auto; SimpleNatRecEq
      ]
    ]
].

Thm Plus/succ/L : [
  (-> [n m : nat] (= nat ($ Plus (succ n) m) (succ ($ Plus n m))))
] by [
  lam n, m => auto; refine fun/eq/app; [`(-> nat nat)]; auto; SimpleNatRecEq
].


Thm Plus/succ/R : [
  (-> [n m : nat] (= nat ($ Plus n (succ m)) (succ ($ Plus n m))))
] by [
  lam n, m => fresh n', n'/ih -> elim n;
    [ auto
    , fresh x -> rewrite ($ Plus/succ/L n' (succ m));
      [ `(= nat x (succ ($ Plus (succ n') m)))
      , fresh x -> rewrite ($ Plus/succ/L n' m);
        [ `(= nat (succ ($ Plus n' (succ m))) (succ x))
        , fresh x -> rewrite-hyp n'/ih;
          [ `(= nat (succ x) (succ (succ ($ Plus n' m))))
          , auto
          ]
        ]
      ]
    ];

    auto
].

Thm Plus/test0 : [
  (-> [n m : nat] [eq : (= nat ($ Plus n zero) m)] (= nat n m))
] by [
  lam n, m, eq =>
    fresh x -> rewrite ($ Plus/zero/R n) in eq;
    [ `(= nat x m) ]; auto
].

Thm Eq/sym : [
  (-> [ty : (U 0)] [a b : ty] (= ty a b) (= ty b a))
] by [
  lam ty, a, b, eq => symmetry; auto
].

Thm Plus/comm : [
  (-> [n m : nat] (= nat ($ Plus n m) ($ Plus m n)))
] by [
  lam n, m => fresh n', n'/ih -> elim n;
    [ symmetry; `($ Plus/zero/R  m)
    , fresh x -> rewrite ($ Plus/succ/L n' m);
      [ `(= nat x ($ Plus m (succ n')))
      , fresh x -> rewrite-hyp n'/ih;
        [ `(= nat (succ x) ($ Plus m (succ n')))
        , symmetry; `($ Plus/succ/R m n')
        ]
      ]
    ];

    auto
].

Thm NatSymm : [
  (->
   [a b : nat]
   (path [_] nat a b)
   (path [_] nat b a))
] by [
  lam a, b, pab => 
  abs i =>
    `(hcom 0~>1 nat a 
      [i=0 [j] (@ pab j)]
      [i=1 [_] a])
].

Print NatSymm.

Thm IntPred : [
  (-> int int)
] by [
  lam a => fresh a', junk -> elim a;
  [ `-1, fresh n, junk -> elim a'; [`0, `(succ n)]
  , `-2, `(negsucc (succ (succ a')))
  ]
].

Thm IntSucc : [
  (-> int int)
] by [
  lam a => fresh a', junk -> elim a;
  [ `1, `(succ (succ a'))
  , `0, `(negsucc a')
  ]
].

Thm IntPlus : [
  (-> int int int)
] by [
  lam a => fresh a', ind -> elim a;
  [ lam b => use b
  , lam b => `($ IntSucc ($ ind b))
  , lam b => `($ IntPred b)
  , lam b => `($ IntPred ($ ind b))
  ]
].

Thm Int4Plus3 : [
  ($ IntPlus 4 3) = 7 in int
] by [ auto ].

Thm Int-6Plus10 : [
  ($ IntPlus -6 10) = 4 in int
] by [ auto ].

Thm Int-1Plus-9 : [
  ($ IntPlus -1 -9) = -10 in int
] by [ auto ].

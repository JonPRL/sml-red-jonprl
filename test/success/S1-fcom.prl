Thm Fcom/trans3 : [
  (-> [a b c d : S1]
   (path [_] S1 a b)
   (path [_] S1 a c)
   (path [_] S1 b d)
   (path [_] S1 c d))
] by [
  lam a b c d pab pac pbd =>
    abs i => `(fcom 0~>1 (@ pab i) [i=0 [j] (@ pac j)] [i=1 [j] (@ pbd j)])
].

Print Fcom/trans3.

Thm Fcom/trans2 : [
  (-> [a b c : S1]
   (path [_] S1 a b)
   (path [_] S1 b c)
   (path [_] S1 a c))
] by [
  lam a b c pab pbc =>
    abs i => `(fcom 0~>1 (@ pab i) [i=0 [_] a] [i=1 [j] (@ pbc j)])
].

Thm Fcom/symm : [
  (-> [a b : S1]
   (path [_] S1 a b)
   (path [_] S1 b a))
] by [
  lam a b pab =>
    abs i => `(fcom 0~>1 a [i=0 [j] (@ pab j)] [i=1 [_] a])
].

Thm Tube : [
  (->
   [x : S1]
   (= S1 (fcom 0~>1 x [1=1 [_] x] [0=0 [_] x]) x))
] by [
  lam x => auto
].

Thm TrueByEvaluation : [
  (fcom 0~>0 base) in S1
] by [
  auto
].

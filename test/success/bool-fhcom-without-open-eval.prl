Thm FHcom/trans3 : [
  (-> [a b c d wbool]
   (path {_} wbool a b)
   (path {_} wbool a c)
   (path {_} wbool b d)
   (path {_} wbool c d))
] by [
  { <a> lam b. lam c. lam d.
    lam pab. lam pac. lam pbd.
    <i> `(fcom{0~>1} (@ ,pab i) [i=0 {j} (@ ,pac j)] [i=1 {j} (@ ,pbd j)])
  };
  auto
].

Print FHcom/trans3.

Thm FHcom/trans2 : [
  (-> [a b c wbool]
   (path {_} wbool a b)
   (path {_} wbool b c)
   (path {_} wbool a c))
] by [
  { lam a. lam b. lam c. lam pab. lam pbc.
    <i> `(fcom{0~>1} (@ ,pab i) [i=0 {_} ,a] [i=1 {j} (@ ,pbc j)])
  };
  auto
].

Thm FHcom/symm : [
  (-> [a b wbool]
   (path {_} wbool a b)
   (path {_} wbool b a))
] by [
  { lam a. lam b. lam pab.
    <i> `(fcom{0~>1} ,a [i=0 {j} (@,pab j)] [i=1 {_} ,a])
  };
  auto
].

Thm Tube : [
  x : wbool
  >> (fcom{0~>1} x [1=1 {_} x] [0=0 {_} x]) = x in wbool
] by [
  auto
].

Thm TrueByEvaluation : [
  (fcom{0 ~> 0} tt) in wbool
] by [
  auto
].

Thm Fcom/trans3 : [
  (-> [a b c d : wbool]
   (path [_] wbool a b)
   (path [_] wbool a c)
   (path [_] wbool b d)
   (path [_] wbool c d))
] by [
  lam a b c d pab pac pbd =>
    abs i => `(fcom 0~>1 (@ pab i) [i=0 [j] (@ pac j)] [i=1 [j] (@ pbd j)])
].

Print Fcom/trans3.

Thm Fcom/trans2 : [
  (-> [a b c : wbool]
   (path [_] wbool a b)
   (path [_] wbool b c)
   (path [_] wbool a c))
] by [
  lam a b c pab pbc =>
    abs i => `(fcom 0~>1 (@ pab i) [i=0 [_] a] [i=1 [j] (@ pbc j)])
].

Thm Fcom/symm : [
  (-> [a b : wbool]
   (path [_] wbool a b)
   (path [_] wbool b a))
] by [
  lam a b pab =>
    abs i => `(fcom 0~>1 a [i=0 [j] (@ pab j)] [i=1 [_] a])
].

Thm Tube : [
  (->
   [x : wbool]
   (= wbool (fcom 0~>1 x [1=1 [_] x] [0=0 [_] x]) x))
] by [
  lam x => auto
].

Thm TrueByEvaluation : [
  (mem wbool (fcom 0~>0 tt))
] by [
  auto
].

structure Ast = RedPrlAst
structure Ar = RedPrlArity
structure Vl = Ar.Vl

open Ast

structure E = RedPrlError
structure K = RedPrlKind
structure O = RedPrlOperator
structure AJ = AtomicJudgment

structure TP = TermPrinter

val ppIntInf = Fpp.text o IntInf.toString

(* helper function *)
(* XXX rewrite this file to use this function more *)
fun \\ (t : ast) = Ast.\ ([], t)

fun makeCustom (opid, bindings) =
  Ast.$$ (O.CUST (opid, NONE), bindings)

fun makeDim pos : IntInf.int -> ast =
  fn 0 => Ast.$$ (O.DIM0, [])
   | 1 => Ast.$$ (O.DIM1, [])
   | i => E.raiseAnnotatedError (pos, E.INVALID_DIMENSION (ppIntInf i))

structure Tac =
struct
  infix $$ $ \

  fun makeSeq mt (us : (string * O.sort) list) mt' =
    let
      val (syms, sorts) = ListPair.unzip us
    in
      O.MTAC_SEQ sorts $$ [[] \ mt, syms \ mt']
    end

  fun multitacToTac mt =
    case out mt of
       O.MTAC_ALL $ [_ \ t] => t
     | _ => setAnnotation (getAnnotation mt) (O.TAC_MTAC $$ [[] \ mt])

  fun tacToMultitac t =
    case out t of
       O.TAC_MTAC $ [_ \ mt] => mt
     | _ => setAnnotation (getAnnotation t) (O.MTAC_ALL $$ [[] \ t])

  fun orElse (t1, t2) =
    multitacToTac (O.MTAC_ORELSE $$ [[] \ tacToMultitac t1, [] \ tacToMultitac t2])

  fun then_ (t1, t2) =
    multitacToTac (makeSeq (tacToMultitac t1) [] (tacToMultitac t2))

  val autoMtac = O.MTAC_AUTO $$ []
  val autoTac = multitacToTac autoMtac

  fun exact m =
    O.RULE_EXACT $$ [[] \ m]

  val fail = O.TAC_FAIL $$ []

  fun try t = 
    orElse (t, O.TAC_ID $$ [])

  fun exactAuto m =
    then_ (exact m, try (then_ (autoTac, fail)))

  fun makeMatch term clauses = 
    let
      fun makeClause t1 t2 = O.DEV_MATCH_CLAUSE $$ [[] \ t1, [] \ t2]
      val clauseArgs = List.map (fn (xs, (t1, t2)) => xs \ makeClause t1 t2) clauses
      val ns = List.map (List.length o #1) clauses
    in
      O.DEV_MATCH ns $$ ([] \ term) :: clauseArgs
    end

  val inversion = 
    O.DEV_INVERSION $$ []

  val assumption = 
    O.TAC_ASSUMPTION $$ []

end

structure Multi =
struct
  infix $$ $ \

  val addUnderscores = List.map (fn SOME x => x | NONE => "_")

  fun makeLam [] m = m
    | makeLam (x::xs) m = O.LAM $$ [[Option.getOpt (x, "_")] \ makeLam xs m]

  fun makeApp m ns =
    List.foldl (fn (n, app) => O.APP $$ [[] \ app, [] \ n]) m ns

  fun makeAbs [] m = m
    | makeAbs (u::us) m = O.ABS $$ [[Option.getOpt (u, "_")] \ makeAbs us m]

  fun makeDimApp m ns =
    List.foldl (fn (r, app) => O.DIM_APP $$ [[] \ app, [] \ r]) m ns

  fun makeNum i =
    case IntInf.compare (i, 0) of
      GREATER => O.SUCC $$ [[] \ makeNum (i - 1)]
    | EQUAL => O.ZERO $$ []
    | LESS => O.NEGSUCC $$ [[] \ makeNum ((~ i) - 1)]

  datatype ('a, 'b) binder = DIM of 'a | TERM of ('a * 'b)

  fun flatten (quant : ('a list,'b) binder list) : ('a,'b) binder list =
    let
      fun expand (DIM vars) = List.map DIM vars
        | expand (TERM (vars, term)) = List.map (fn var => TERM (var, term)) vars
    in
      ListUtil.concatMap expand quant
    end

  fun makeFunOrLineAux [] cod = cod
    | makeFunOrLineAux (TERM (x, a) :: doms) cod =
       O.FUN $$ [[] \ a, [Option.getOpt (x, "_")] \ makeFunOrLineAux doms cod]
    | makeFunOrLineAux (DIM x :: doms) cod =
       O.LINE $$ [[Option.getOpt (x, "_")] \ makeFunOrLineAux doms cod]

  val makeFunOrLine = makeFunOrLineAux o flatten

  structure LabelSet = SplaySet (structure Elem = StringOrdered)


  fun checkNoDuplicates pos lbls =
    let
      fun insert (lbl, set) =
        if LabelSet.member set lbl then
          E.raiseAnnotatedError (pos, E.GENERIC [Fpp.text "Duplicate label", Fpp.text lbl])
        else
          LabelSet.insert set lbl
    in
      List.foldl insert LabelSet.empty lbls;
      ()
    end

  fun makeRecordAux pos tbs =
    let
      val fields = ListUtil.mapWithIndex
        (fn (_, TERM (SOME x, ty)) => (x, ty)
          | (i, TERM (NONE, ty)) => (O.indexToLabel i, ty)
          | (_, DIM _) => E.raiseAnnotatedError (pos, E.GENERIC [Fpp.text "No dimension binders in record types."]))
        (flatten tbs)
      val init = {labels = [], args = []}
      val {labels, args} =
        List.foldl
          (fn ((lbl, ty), {labels, args}) =>
            {labels = labels @ [lbl],
             args = (labels \ ty) :: args})
          init
          fields

      val lbls = labels
      val _ = checkNoDuplicates pos lbls
    in
      (lbls, List.rev args)
    end

  fun makeProd pos (tbs, ty) =
    let
      val (_, args) = makeRecordAux pos (tbs @ [TERM ([NONE], ty)])
      val lbls = ListUtil.mapWithIndex (O.indexToLabel o #1) args
    in
      O.RECORD lbls $$ args
    end

  fun makeRecord pos tbs =
    let
      val (lbls, args) = makeRecordAux pos tbs
    in
      O.RECORD lbls $$ args
    end

  fun makeTuple pos fields =
    let
      val (lbls, args) = ListPair.unzip fields
    in
      checkNoDuplicates pos lbls;
      O.TUPLE lbls $$ List.map (fn a => [] \ a) args
    end

  fun makeAnonymousTuple terms =
    let
      val lbls = ListUtil.mapWithIndex (O.indexToLabel o #1) terms
    in
      O.TUPLE lbls $$ List.map (fn a => [] \ a) terms
    end

  fun recordIntro pos fields =
    let
      val (lbls, tacs) = ListPair.unzip fields
    in
      checkNoDuplicates pos lbls;
      O.DEV_RECORD_INTRO lbls $$ List.map (fn tac => [] \ tac) tacs
    end


  fun sigmaIntro tacs =
    let
      val lbls = List.tabulate (List.length tacs, O.indexToLabel)
    in
      O.DEV_RECORD_INTRO lbls $$ List.map (fn tac => [] \ tac) tacs
    end

  fun joinAnnotations (xs : ast list) : Pos.t option = 
    case xs of
       [] => NONE
     | x :: xs => 
       case (Ast.getAnnotation x, joinAnnotations xs) of
          (SOME p1, SOME p2) => SOME (Pos.union p1 p2)
        | (SOME p, NONE) => SOME p
        | (NONE, SOME p) => SOME p
        | _ => NONE
 

  fun makeVec tau xs =
    Ast.setAnnotation (joinAnnotations xs) (O.MK_VEC (tau, List.length xs) $$ List.map (fn x => [] \ x) xs)
end

structure Pattern =
struct
  infix $$ $ \

  fun labelAnonTuplePattern pats = 
    let
      val lbls = List.tabulate (List.length pats, O.indexToLabel)
    in
      ListPair.zip (lbls, pats)
    end

  (* this code is kind of crappy, feel free to improve it *)
  fun unstitchPattern (pat : 'a O.dev_pattern) : unit O.dev_pattern * 'a list =
    case pat of
       O.PAT_VAR a => (O.PAT_VAR (), [a])
     | O.PAT_TUPLE lpats =>
       let
         val (lbls, pats) = ListPair.unzip (List.map (fn (lbl, pat) => (lbl, unstitchPattern pat)) lpats)
         val (pats, names) = ListPair.unzip pats
       in
         (O.PAT_TUPLE (ListPair.zip (lbls, pats)), List.concat names)
       end

  fun makeLambda pats tac =
    let
      val (pats', namess) = ListPair.unzip (List.map unstitchPattern pats)
      val names = List.concat namess
    in
      O.DEV_FUN_INTRO pats' $$ [names \ tac]
    end

  fun makeApplyHyp pat z tacs tac =
    let
      val (pat, names) = unstitchPattern pat
      val vec = Multi.makeVec O.TAC tacs
    in
      O.DEV_APPLY_HYP pat $$ [[] \ z, [] \ vec, names \ tac]
    end

  fun makeUseHyp z tacs = 
    let
      val vec = Multi.makeVec O.TAC tacs
    in
      O.DEV_USE_HYP $$ [[] \ z, [] \ vec]
    end

  fun makeApplyLemma pat (opid, args) tacs tac =
    let
      val cust = O.CUST (opid, NONE) $$ args
      val any = O.MK_ANY NONE $$ [[] \ cust]
      val (pat, names) = unstitchPattern pat
      val vec = Multi.makeVec O.TAC tacs
      val args = [[] \ any, [] \ vec, names \ tac]
    in
      O.DEV_APPLY_LEMMA pat $$ args
    end

  fun makeUseLemma (opid, args) tacs = 
    let
      val cust = O.CUST (opid, NONE) $$ args
      val any = O.MK_ANY NONE $$ [[] \ cust]
      val vec = Multi.makeVec O.TAC tacs
    in
      O.DEV_USE_LEMMA $$ [[] \ any, [] \ vec]
    end
end

%%
%header (functor RedPrlLrValsFun (structure Token : TOKEN))

%term
   EOF

 | VARNAME of string
 | OPNAME of string
 | UNDER
 | NUMERAL of IntInf.int
 | HOLENAME of string

 | COLON
 (* delimiters *)
 | LANGLE | RANGLE
 | LANGLE_PIPE | RANGLE_PIPE
 | LPAREN | RPAREN
 | RBRACKET | LBRACKET
 | LSQUARE | RSQUARE
 (* (mostly) single-character variables *)
 | BACK_TICK
 | COMMA
 | AMPERSAND
 | DOT
 | EQUALS
 | HASH
 | PERCENT
 | PIPE | DOUBLE_PIPE
 | SEMI
 (* arrows *)
 | SQUIGGLE_RIGHT_ARROW | SQUIGGLE_LEFT_ARROW
 | RIGHT_ARROW | LEFT_ARROW
 | DOUBLE_RIGHT_ARROW | LONG_RIGHT_ARROW
 | TRIANGLE_RIGHT
 | PLUS | DOUBLE_PLUS

 (* sorts *)
 | DIM | EXP | TAC | JDG | LVL | KND


 (* keywords and variables in expressions *)
 | AX
 | FCOM
 | BOOL | TT | FF | IF
 | WBOOL | WIF
 | NAT | ZERO | SUCC | NAT_REC
 | INT | NEGSUCC | INT_REC
 | VOID
 | S1 | BASE | LOOP | S1_REC
 | LAMBDA | DOLLAR_SIGN
 | RECORD | TIMES | TUPLE | BANG
 | PATH | LINE | ABS | AT_SIGN
 | PUSHOUT | LEFT | RIGHT | GLUE | PUSHOUT_REC
 | COEQUALIZER | CECOD | CEDOM | COEQUALIZER_REC
 | MEM
 | BOX | CAP
 | V | VIN | VPROJ
 | UNIVERSE
 | HCOM | COE | COM

 | LMAX

 (* keywords in tactics *)
 | CASE | OF
 | FRESH
 | LET | CLAIM | USE | WITH
 | THEN | ELSE
 | REFINE
 | MTAC_PROGRESS | MTAC_REPEAT | MTAC_AUTO | MTAC_HOLE
 | TAC_ID | TAC_FAIL | TAC_AUTO_STEP | TAC_SYMMETRY | TAC_ELIM | TAC_REWRITE | TAC_REDUCE | TAC_UNFOLD
 | RULE_EXACT
 | TAC_INVERSION
 | TAC_ASSUMPTION
 | MATCH
 | QUERY | CONCL
 | PRINT | TRACE

 (* keywords in judgments *)
 | TRUE | TYPE | SYNTH | AT (* `WITH` is reused *)

 | DISCRETE | KAN | STABLE

 (* commands and declarations *)
 | CMD_PRINT | CMD_EXTRACT | CMD_QUIT
 | DCL_DEF | DCL_TAC | DCL_THM
 | BY | IN

%right LEFT_ARROW RIGHT_ARROW DOUBLE_PIPE SEMI
%right TIMES
%nonassoc COMMA AMPERSAND COLON
%nonassoc FCOM BOOL TT FF IF WBOOL WIF NAT ZERO SUCC NAT_REC INT NEGSUCC INT_REC VOID S1 BASE LOOP LAMBDA DOLLAR_SIGN PATH ABS AT_SIGN AX HCOM COE
%nonassoc HASH LANGLE LPAREN VARNAME OPNAME



%nonterm
   start of Signature.Src.elt

 | ident of string
 | boundVar of string option
 | boundVars of string option list

 | sort of O.sort
 | sorts of O.sort list

 | bindingSig of O.sort list
 | valence of Ar.valence

   (* parameters *)
 | numAsDimension of ast
 | dimension of ast
 | dimensions of ast list
 | dir of ast * ast
 | backDir of ast * ast
 | equation of ast * ast
 | tube of ast
 | tubes of ast list
 | boundary of ast
 | boundaries of ast list
 | tubeVec of ast
 | boundaryVec of ast

 | metavar of string
 | patvar of string * O.sort
 | patvarBindings of string list
 | customOpTerm of MlId.t * ast abs list


 | typedBinder of (string option list, ast) Multi.binder
 | typedBinders of (string option list, ast) Multi.binder list
 | dimBinder of (string option list, ast) Multi.binder
 | arrowQuantifierData of (string option list, ast) Multi.binder list * ast
 | timesQuantifierData of (string option list, ast) Multi.binder list * ast
 | field of string * ast
 | fields of (string * ast) list

   (* abt abstractions *)
 | binder of string list
 | binding of ast abs
 | bindings of ast abs list

   (* a type-theoretic term *)
 | nullaryOperator of operator
 | multinaryOperator of operator
 | rawTermAndTac of ast (* shared with tactics *)
 | rawTerm of ast
 | level of ast
 | levels of ast list

   (* a type-theoretic term, annotated with source position *)
 | term of ast
 | terms of ast list

 | kind of K.kind
 | withKind of ast

 | rawJudgment of ast
 | judgment of ast

 | src_atjdg of ast

 | termAnySort of ast
 | termsAnySort of ast list
 | elimTarget of ast
 | elimTargetAnySort of ast

 | atomicRawTac of ast
 | atomicTac of ast
 | atomicRawMultitac of ast
 | atomicMultitac of ast
 | rawMultitac of ast
 | multitac of ast
 | hypBinding of string * O.sort
 | hypBindings of (string * O.sort) list
 | rawTactic of ast
 | tactic of ast
 | tactics of ast list

 | recordFieldTactics of (string * ast) list

 | devAppSpine of ast list
 | bracketedDevAppSpine of ast list
 | devDecompPattern of string O.dev_pattern
 | devDecompPatterns of string O.dev_pattern list
 | labeledDecompPattern of string * string O.dev_pattern
 | anonTupleDecompPattern of string O.dev_pattern list
 | labeledTupleDecompPattern of (string * string O.dev_pattern) list
 | tupleDecompPattern of (string * string O.dev_pattern) list
 | devMatchClause of string list * (ast * ast)
 | devMatchClauses of (string list * (ast * ast)) list
 | selector of ast
 | selectors of ast list
 | accessor of ast
 | accessors of ast list
 | opnames of MlId.t list

 | declArgument of metavariable * Ar.valence
 | declArguments of Signature.Src.arguments
 | declArgumentsParens of Signature.Src.arguments

   (* a declaration, annotated with source position *)
 | decl of MlId.t * Signature.Src.decl * Pos.t
   (* a RedPRL signature *)
 | rawCmd of Signature.Src.cmd
 | cmd of Signature.Src.cmd * Pos.t
 | elt of Signature.Src.elt

%verbose
%pos (string -> Coord.t)
%start start
%eop EOF DOT
%noshift EOF
%name RedPrl
%arg (fileName) : string
%%

start : elt (elt)

ident
  : OPNAME (OPNAME)
  | VARNAME (VARNAME)

boundVar
  : VARNAME (SOME VARNAME)
  | UNDER (NONE)

boundVars
  : boundVar ([boundVar])
  | boundVar boundVars (boundVar :: boundVars)

sort
  : EXP (O.EXP)
  | TAC (O.TAC)
  | JDG (O.JDG)
  | LVL (O.LVL)
  | KND (O.KND)
  | DIM (O.DIM)

sorts
  : sort ([sort])
  | sort COMMA sorts (sort :: sorts)
  | ([])

bindingSig
  : LSQUARE sorts RSQUARE DOT (sorts)
  | ([])

valence
  : bindingSig sort ((bindingSig, sort))

numAsDimension
  : NUMERAL (makeDim (Pos.pos (NUMERAL1left fileName) (NUMERAL1right fileName)) NUMERAL)

dimension
  : VARNAME (annotate (Pos.pos (VARNAMEleft fileName) (VARNAMEright fileName)) (`` VARNAME))
  | metavar  (annotate (Pos.pos (metavarleft fileName) (metavarright fileName)) (Ast.$$# (metavar, [])))
  | numAsDimension (annotate (Pos.pos (numAsDimensionleft fileName) (numAsDimensionright fileName)) numAsDimension)

dimensions
  : dimension dimensions (dimension :: dimensions)
  | dimension ([dimension])

dir
  : dimension SQUIGGLE_RIGHT_ARROW dimension ((dimension1, dimension2))

backDir
  : dimension SQUIGGLE_LEFT_ARROW dimension ((dimension1, dimension2))

equation
  : dimension EQUALS dimension (dimension1, dimension2)

tube
  : LSQUARE equation binding RSQUARE (Ast.$$ (O.MK_TUBE, [\ ([], #1 equation), \ ([], #2 equation), binding]))

boundary
  : LSQUARE equation binding RSQUARE (Ast.$$ (O.MK_BDRY, [\ ([], #1 equation), \ ([], #2 equation), binding]))

tubes
  : tube tubes (tube :: tubes)
  | ([])

boundaries
  : boundary boundaries (boundary :: boundaries)
  | ([])

tubeVec
  : tubes (Multi.makeVec O.TUBE tubes)

boundaryVec
  : boundaries (Multi.makeVec O.BDRY boundaries)

metavar
  : HASH ident (ident)

typedBinder
  : LSQUARE boundVars COLON term RSQUARE (Multi.TERM (boundVars, term))

typedBinders
  : typedBinder ([typedBinder])
  | typedBinder typedBinders (typedBinder :: typedBinders)

dimBinder
  : LSQUARE boundVars COLON DIM RSQUARE (Multi.DIM boundVars)

arrowQuantifierData
  : typedBinder arrowQuantifierData ((typedBinder :: #1 arrowQuantifierData), #2 arrowQuantifierData)
  | dimBinder arrowQuantifierData ((dimBinder :: #1 arrowQuantifierData), #2 arrowQuantifierData)
  | term arrowQuantifierData ((Multi.TERM ([NONE], term) :: #1 arrowQuantifierData), #2 arrowQuantifierData)
  | DIM arrowQuantifierData ((Multi.DIM [NONE] :: #1 arrowQuantifierData), #2 arrowQuantifierData)
  | term ([], term)

timesQuantifierData
  : typedBinder timesQuantifierData ((typedBinder :: #1 timesQuantifierData), #2 timesQuantifierData)
  | term timesQuantifierData ((Multi.TERM ([NONE], term) :: #1 timesQuantifierData), #2 timesQuantifierData)
  | term ([], term)

field
  : LSQUARE ident term RSQUARE (ident, term)

fields
  : field fields (field :: fields)
  | field ([field])

binder
  : LSQUARE boundVars RSQUARE (Multi.addUnderscores boundVars)

binding
  : binder term (\ (binder, term))
  | term (\ ([], term))

bindings
  : binding bindings (binding :: bindings)
  | binding ([binding])


(* built-in nullary operators that take no parameters *)
nullaryOperator
  (* axiom *)
  : AX (O.AX)
  (* bools *)
  | BOOL (O.BOOL)
  | TT (O.TT)
  | FF (O.FF)
  (* weak bools *)
  | WBOOL (O.WBOOL)
  (* natural numbers *)
  | NAT (O.NAT)
  | ZERO (O.ZERO)
  (* integers *)
  | INT (O.INT)
  (* void *)
  | VOID (O.VOID)
  (* circle *)
  | S1 (O.S1)
  | BASE (O.BASE)

  | kind (O.KCONST kind)


(* built-in operators that do not have special form *)
multinaryOperator
  (* bools *)
  : IF (O.IF)
  (* weak bools *)
  | WIF (O.WIF)
  (* natural numbers *)
  | SUCC (O.SUCC)
  | NAT_REC (O.NAT_REC)
  (* integers *)
  | NEGSUCC (O.NEGSUCC)
  | INT_REC (O.INT_REC)
  (* S1 *)
  | S1_REC (O.S1_REC)
  (* paths *)
  | PATH (O.PATH)
  (* lines *)
  | LINE (O.LINE)
  (* pushouts *)
  | PUSHOUT (O.PUSHOUT)
  | LEFT (O.LEFT)
  | RIGHT (O.RIGHT)
  | GLUE (O.GLUE)
  | PUSHOUT_REC (O.PUSHOUT_REC)
  (* coequalizers *)
  | COEQUALIZER (O.COEQUALIZER)
  | CECOD (O.CECOD)
  | CEDOM (O.CEDOM)
  | COEQUALIZER_REC (O.COEQUALIZER_REC)
  (* equality *)
  | EQUALS (O.EQUALITY)
  (* V *)
  | V (O.V)
  | VIN (O.VIN)
  | VPROJ (O.VPROJ)


customOpTerm
  : OPNAME (MlId.const OPNAME, [])
  | LPAREN OPNAME bindings RPAREN (MlId.const OPNAME, bindings)

patvar
  : PERCENT LSQUARE VARNAME COLON sort RSQUARE ((VARNAME, sort))
  | PERCENT VARNAME ((VARNAME, O.EXP))

patvarBindings
  : VARNAME patvarBindings (VARNAME :: patvarBindings)
  | VARNAME ([VARNAME])

rawTermAndTac
  : VARNAME (`` VARNAME)

  (* meta variable *)
  | metavar (Ast.$$# (metavar, []))
  | LPAREN metavar terms RPAREN (Ast.$$# (metavar, terms))

  (* pattern variables *)
  | patvar (Ast.$$ (O.PAT_META (#2 patvar), [\ ([], ``(#1 patvar)), \ ([], Multi.makeVec O.ANY [])]))
  | LPAREN patvar termsAnySort RPAREN (Ast.$$ (O.PAT_META (#2 patvar), [\ ([], ``(#1 patvar)), \ ([], Multi.makeVec O.ANY termsAnySort)]))

  (* custom operators *)
  | customOpTerm (makeCustom customOpTerm)

  (* built-in operators that can be handled uniformly *)
  | nullaryOperator (Ast.$$ (nullaryOperator, []))
  | LPAREN multinaryOperator bindings RPAREN (Ast.$$ (multinaryOperator, bindings))

  | LPAREN UNIVERSE level RPAREN (Ast.$$ (O.UNIVERSE, [Ast.\ ([], level), Ast.\ ([], Ast.$$ (O.KCONST RedPrlKind.top, []))]))
  | LPAREN UNIVERSE level term RPAREN (Ast.$$ (O.UNIVERSE, [Ast.\ ([], level), Ast.\ ([], term)]))

  | LPAREN LOOP dimension RPAREN (Ast.$$ (O.LOOP, [Ast.\ ([], dimension)]))

  (* Kan operators *)
  | LPAREN COE dir binding term RPAREN
    (Ast.$$ (O.COE, [Ast.\ ([], #1 dir), Ast.\ ([], #2 dir), binding, Ast.\ ([], term)]))
  | LPAREN FCOM dir binding tubeVec RPAREN
    (Ast.$$ (O.FCOM, [\ ([], #1 dir), \([], #2 dir), binding, \([], tubeVec)]))
  | LPAREN HCOM dir binding binding tubeVec RPAREN
    (Ast.$$ (O.HCOM, [\ ([], #1 dir), \([], #2 dir), binding1, binding2, \([], tubeVec)]))
  | LPAREN COM dir binding binding tubeVec RPAREN
    (Ast.$$ (O.COM, [\ ([], #1 dir), \([], #2 dir), binding1, binding2, \([], tubeVec)]))
  | LPAREN BOX dir binding boundaryVec RPAREN
    (Ast.$$ (O.BOX, [\ ([], #1 dir), \([], #2 dir), binding, \([], boundaryVec)]))
  | LPAREN CAP backDir binding tubeVec RPAREN
    (Ast.$$ (O.CAP, [\ ([], #1 backDir), \([], #2 backDir), binding, \([], tubeVec)]))

  (* numbers and dimensions *)
  | NUMERAL (Multi.makeNum NUMERAL)
  | LPAREN DIM dimension RPAREN (dimension)

  (* function types *)
  | LPAREN RIGHT_ARROW arrowQuantifierData RPAREN (Multi.makeFunOrLine (#1 arrowQuantifierData) (#2 arrowQuantifierData))
  | LPAREN LAMBDA LSQUARE boundVars RSQUARE term RPAREN (Multi.makeLam boundVars term)
  | LPAREN DOLLAR_SIGN term terms RPAREN (Multi.makeApp term terms)

  (* pair types *)
  | LPAREN TIMES timesQuantifierData RPAREN (Multi.makeProd (Pos.pos (LPAREN1left fileName) (RPAREN1right fileName)) timesQuantifierData)

  (* dependent record types *)
  | RECORD (Multi.makeRecord (Pos.pos (RECORD1left fileName) (RECORD1right fileName)) [])
  | LPAREN RECORD typedBinders RPAREN (Multi.makeRecord (Pos.pos (LPAREN1left fileName) (RPAREN1right fileName)) typedBinders)
  | TUPLE (Multi.makeTuple (Pos.pos (TUPLE1left fileName) (TUPLE1right fileName)) [])
  | LPAREN TUPLE fields RPAREN (Multi.makeTuple (Pos.pos (LPAREN1left fileName) (RPAREN1right fileName)) fields)
  | LPAREN TUPLE terms RPAREN (Multi.makeAnonymousTuple terms)
  | LPAREN BANG ident term RPAREN (Ast.$$ (O.PROJ ident, [\ ([], term)]))

  (* paths *)
  | LPAREN ABS LSQUARE boundVars RSQUARE term RPAREN (Multi.makeAbs boundVars term)
  | LPAREN AT_SIGN term dimensions RPAREN (Multi.makeDimApp term dimensions)

  (* Abbreviation for membership *)
  | LPAREN MEM term term RPAREN (Ast.$$ (O.EQUALITY, [Ast.\ ([], term1), Ast.\ ([], term2), Ast.\ ([], term2)]))

  (* levels *)
  | LPAREN PLUS level NUMERAL RPAREN (Ast.$$ (O.LPLUS NUMERAL, [Ast.\ ([], level)]))
  | LPAREN DOUBLE_PLUS level RPAREN (Ast.$$ (O.LPLUS 1, [Ast.\ ([], level)]))
  | LPAREN LMAX levels RPAREN (Ast.$$ (O.LMAX, [\ ([], Multi.makeVec O.LVL levels)]))


level
  : NUMERAL (Ast.$$ (O.LCONST NUMERAL, []))
  | LPAREN PLUS level NUMERAL RPAREN (Ast.$$ (O.LPLUS NUMERAL, [Ast.\ ([], level)]))
  | LPAREN DOUBLE_PLUS level RPAREN (Ast.$$ (O.LPLUS 1, [Ast.\ ([], level)]))
  | LPAREN LMAX levels RPAREN (Ast.$$ (O.LMAX, [\ ([], Multi.makeVec O.LVL levels)]))
  | metavar (Ast.$$# (metavar, []))
  | PERCENT VARNAME (Ast.$$ (O.PAT_META O.LVL, [\ ([], ``VARNAME), \ ([], Multi.makeVec O.ANY [])]))

levels
  : level ([level])
  | levels level (level :: levels)

rawTerm
  : rawTermAndTac (rawTermAndTac)
  | HASH TAC LBRACKET tactic RBRACKET (tactic)
  | HASH JDG LBRACKET judgment RBRACKET (judgment)
  | HASH LVL LBRACKET level RBRACKET (level)

term : rawTerm (annotate (Pos.pos (rawTerm1left fileName) (rawTerm1right fileName)) rawTerm)

terms
  : term ([term])
  | term terms (term :: terms)

kind
  : DISCRETE (K.DISCRETE)
  | KAN (K.KAN)
  | HCOM (K.HCOM)
  | COE (K.COE)
  | STABLE (K.STABLE)

withKind
  : WITH term (term)
  | (Ast.$$ (O.KCONST K.top, []))

rawJudgment
  : term EQUALS term IN term (Ast.$$ (O.JDG_TRUE, [\\ (Ast.$$ (O.EQUALITY, [\\ term3, \\ term1, \\ term2]))]))
  | term IN term (Ast.$$ (O.JDG_TRUE, [\\ (Ast.$$ (O.EQUALITY, [\\ term2, \\ term1, \\ term1]))]))
  | term TRUE (Ast.$$ (O.JDG_TRUE, [\\ term]))
  | term (Ast.$$ (O.JDG_TRUE, [\\ term]))
  | term EQUALS term TYPE withKind (Ast.$$ (O.JDG_EQ_TYPE, [\\ withKind, \\ term1, \\ term2]))
  | term TYPE withKind (Ast.$$ (O.JDG_EQ_TYPE, [\\ withKind, \\ term, \\ term]))
  | term SYNTH (Ast.$$ (O.JDG_SYNTH, [\\ term]))

judgment : rawJudgment (annotate (Pos.pos (rawJudgment1left fileName) (rawJudgment1right fileName)) rawJudgment)

src_atjdg : judgment (judgment)

recordFieldTactics
   : VARNAME EQUALS tactic COMMA recordFieldTactics ((VARNAME, tactic) :: recordFieldTactics)
   | VARNAME EQUALS tactic ([(VARNAME, tactic)])
   | ([])

devAppSpine
  : tactic COMMA devAppSpine (tactic :: devAppSpine)
  | tactic ([tactic])
  | ([])

bracketedDevAppSpine
  : LSQUARE devAppSpine RSQUARE (devAppSpine)
  | ([])

devDecompPattern
  : VARNAME (O.PAT_VAR VARNAME)
  | UNDER (O.PAT_VAR "_")
  | LBRACKET tupleDecompPattern RBRACKET (O.PAT_TUPLE tupleDecompPattern)

devDecompPatterns
  : devDecompPattern devDecompPatterns (devDecompPattern :: devDecompPatterns)
  | devDecompPattern ([devDecompPattern])

labeledDecompPattern
  : VARNAME EQUALS devDecompPattern (VARNAME, devDecompPattern)

labeledTupleDecompPattern
   : labeledDecompPattern COMMA labeledTupleDecompPattern (labeledDecompPattern :: labeledTupleDecompPattern)
   | labeledDecompPattern ([labeledDecompPattern])
   | ([])

anonTupleDecompPattern
   : devDecompPattern COMMA anonTupleDecompPattern (devDecompPattern :: anonTupleDecompPattern)
   | devDecompPattern ([devDecompPattern])

tupleDecompPattern
  : labeledTupleDecompPattern (labeledTupleDecompPattern)
  | anonTupleDecompPattern (Pattern.labelAnonTuplePattern anonTupleDecompPattern)

devMatchClause
  : LSQUARE patvarBindings PIPE termAnySort DOUBLE_RIGHT_ARROW tactic RSQUARE (patvarBindings, (termAnySort, tactic))
  | LSQUARE termAnySort DOUBLE_RIGHT_ARROW tactic RSQUARE ([], (termAnySort, tactic))

devMatchClauses
  : devMatchClause devMatchClauses (devMatchClause :: devMatchClauses)
  | ([])

selector
  : CONCL (Ast.$$ (O.SEL_CONCL, []))
  | elimTargetAnySort (Ast.$$ (O.SEL_HYP, [Ast.\ ([], elimTargetAnySort)]))

accessor
  : TYPE (Ast.$$ (O.ACC_TYPE, []))
  | LEFT (Ast.$$ (O.ACC_LEFT, []))
  | RIGHT (Ast.$$ (O.ACC_RIGHT, []))

accessors
  : accessor accessors (accessor :: accessors)
  | accessor ([accessor])

selectors
  : selector selectors (selector :: selectors)
  | selector ([selector])

opnames
  : OPNAME opnames (MlId.const OPNAME :: opnames)
  | OPNAME ([MlId.const OPNAME])

termAnySort
  : term (Ast.setAnnotation (Ast.getAnnotation term) (Ast.$$ (O.MK_ANY NONE, [Ast.\ ([], term)])))

termsAnySort
  : termAnySort ([termAnySort])
  | termAnySort termsAnySort (termAnySort :: termsAnySort)

elimTarget
  : VARNAME (annotate (Pos.pos (VARNAMEleft fileName) (VARNAMEright fileName)) (``VARNAME))
  | metavar (annotate (Pos.pos (metavarleft fileName) (metavarright fileName)) (Ast.$$# (metavar, [])))

elimTargetAnySort
  : elimTarget (Ast.setAnnotation (Ast.getAnnotation elimTarget) (Ast.$$ (O.MK_ANY NONE, [Ast.\ ([], elimTarget)])))


atomicRawTac
  : REFINE VARNAME (Ast.$$ (O.RULE_PRIM VARNAME, []))
  | TAC_ID (Ast.$$ (O.TAC_ID, []))
  | TAC_FAIL (Ast.$$ (O.TAC_FAIL, []))
  | TAC_AUTO_STEP (Ast.$$ (O.TAC_AUTO_STEP, []))
  | TAC_SYMMETRY (Ast.$$ (O.TAC_SYMMETRY, []))
  | TAC_ELIM elimTargetAnySort (Ast.$$ (O.TAC_ELIM, [Ast.\ ([], elimTargetAnySort)]))
  | TAC_REWRITE term (Ast.$$ (O.TAC_REWRITE, [\\ (Ast.$$ (O.SEL_CONCL, [])), \\ (Ast.$$ (O.ACC_WHOLE, [])), \\ term]))
  | TAC_REWRITE term AT accessor (Ast.$$ (O.TAC_REWRITE, [\\ (Ast.$$ (O.SEL_CONCL, [])), \\ accessor, \\ term]))
  | TAC_REWRITE term IN selector (Ast.$$ (O.TAC_REWRITE, [\\ selector, \\ (Ast.$$ (O.ACC_WHOLE, [])), \\ term]))
  | TAC_REWRITE term AT accessor IN selector (Ast.$$ (O.TAC_REWRITE, [\\ selector, \\ accessor, \\ term]))
  | TAC_REWRITE term IN selector AT accessor (Ast.$$ (O.TAC_REWRITE, [\\ selector, \\ accessor, \\ term]))
  | TAC_REDUCE (Ast.$$ (O.TAC_REDUCE_ALL, []))
  | TAC_REDUCE IN selectors (Ast.$$ (O.TAC_REDUCE, [Ast.\ ([], Multi.makeVec O.SEL selectors)]))
  | TAC_REDUCE AT accessors (Ast.$$ (O.TAC_REDUCE_PART, [\\ (Ast.$$ (O.SEL_CONCL, [])), \\ (Multi.makeVec O.ACC accessors)]))
  | TAC_REDUCE IN selector AT accessors (Ast.$$ (O.TAC_REDUCE_PART, [\\ selector, \\ (Multi.makeVec O.ACC accessors)]))
  | TAC_REDUCE AT accessors IN selector (Ast.$$ (O.TAC_REDUCE_PART, [\\ selector, \\ (Multi.makeVec O.ACC accessors)]))
  | TAC_UNFOLD opnames (Ast.$$ (O.TAC_UNFOLD_ALL opnames, []))
  | TAC_UNFOLD opnames IN selectors (Ast.$$ (O.TAC_UNFOLD (opnames), [Ast.\ ([], Multi.makeVec O.SEL selectors)]))

  | BACK_TICK termAnySort (Tac.exactAuto termAnySort)
  | RULE_EXACT termAnySort (Tac.exact termAnySort)

  | TAC_INVERSION (Tac.inversion)
  | TAC_ASSUMPTION (Tac.assumption)

  | atomicTac DOUBLE_PIPE tactic %prec DOUBLE_PIPE (Tac.orElse (atomicTac, tactic))

  | LAMBDA devDecompPatterns DOUBLE_RIGHT_ARROW tactic (Pattern.makeLambda devDecompPatterns tactic)
  | ABS boundVars DOUBLE_RIGHT_ARROW tactic (Ast.$$ (O.DEV_PATH_INTRO (List.length boundVars), [\ (Multi.addUnderscores boundVars, tactic)]))
  | LBRACKET recordFieldTactics RBRACKET (Multi.recordIntro (Pos.pos (LBRACKET1left fileName) (RBRACKET1right fileName)) recordFieldTactics)
  | LBRACKET tactics RBRACKET (Multi.sigmaIntro tactics)
  
  | IF elimTarget THEN tactic ELSE tactic
      (Ast.$$ (O.DEV_BOOL_ELIM, [\ ([], elimTarget), \ ([], tactic1), \ ([], tactic2)]))

  | CLAIM VARNAME COLON LSQUARE judgment RSQUARE BY LSQUARE tactic RSQUARE SEMI tactic
      (Ast.$$ (O.DEV_LET NONE, [\ ([], judgment), \ ([], tactic1), \([VARNAME], tactic2)]))

  | LET devDecompPattern EQUALS elimTargetAnySort bracketedDevAppSpine SEMI tactic
      (Pattern.makeApplyHyp devDecompPattern elimTargetAnySort bracketedDevAppSpine tactic)

  | LET devDecompPattern EQUALS customOpTerm bracketedDevAppSpine SEMI tactic (Pattern.makeApplyLemma devDecompPattern customOpTerm bracketedDevAppSpine tactic)

  | USE elimTargetAnySort bracketedDevAppSpine (Pattern.makeUseHyp elimTargetAnySort bracketedDevAppSpine)
  | USE customOpTerm bracketedDevAppSpine (Pattern.makeUseLemma customOpTerm bracketedDevAppSpine)

  | CASE elimTarget OF BASE DOUBLE_RIGHT_ARROW tactic PIPE LOOP VARNAME DOUBLE_RIGHT_ARROW tactic
      (Ast.$$ (O.DEV_S1_ELIM, [\ ([], elimTarget), \ ([], tactic1), \([VARNAME], tactic2)]))

  | MATCH termAnySort LBRACKET devMatchClauses RBRACKET (Tac.makeMatch termAnySort devMatchClauses)
  | QUERY VARNAME LEFT_ARROW selector SEMI tactic (Ast.$$ (O.DEV_QUERY, [\ ([], selector), \ ([VARNAME], tactic)]))
  | PRINT termAnySort (Ast.$$ (O.DEV_PRINT, [\ ([], termAnySort)]))
  | TRACE VARNAME (Ast.$$ (O.TAC_TRACE VARNAME, []))

  (* overlapping with term *)
  | rawTermAndTac (annotate (Pos.pos (rawTermAndTacleft fileName) (rawTermAndTacright fileName)) rawTermAndTac)

atomicTac : atomicRawTac (annotate (Pos.pos (atomicRawTac1left fileName) (atomicRawTac1right fileName)) atomicRawTac)

atomicRawMultitac
  : LSQUARE tactics RSQUARE (Ast.$$ (O.MTAC_EACH, [\([], Multi.makeVec O.TAC tactics)]))
  | HASH NUMERAL LBRACKET tactic RBRACKET (Ast.$$ (O.MTAC_FOCUS (IntInf.toInt NUMERAL), [\ ([], tactic)]))
  | MTAC_REPEAT LBRACKET multitac RBRACKET (Ast.$$ (O.MTAC_REPEAT, [\ ([], multitac)]))
  | MTAC_AUTO (Tac.autoMtac)
  | MTAC_PROGRESS LBRACKET multitac RBRACKET (Ast.$$ (O.MTAC_PROGRESS, [\ ([], multitac)]))
  | LPAREN multitac RPAREN (multitac)
  | FRESH hypBindings RIGHT_ARROW atomicRawMultitac %prec RIGHT_ARROW (Tac.makeSeq atomicRawMultitac hypBindings (Tac.tacToMultitac (Ast.$$ (O.TAC_ID, []))))
  | FRESH hypBindings RIGHT_ARROW atomicRawMultitac SEMI multitac %prec RIGHT_ARROW (Tac.makeSeq atomicRawMultitac hypBindings multitac)
  | atomicTac %prec SEMI (Ast.$$ (O.MTAC_ALL, [\ ([], atomicTac)]))
  | HOLENAME (Ast.$$ (O.MTAC_HOLE (SOME HOLENAME), []))

atomicMultitac
  : atomicRawMultitac (annotate (Pos.pos (atomicRawMultitac1left fileName) (atomicRawMultitac1right fileName)) atomicRawMultitac)

rawMultitac
  : multitac SEMI atomicMultitac %prec SEMI (Tac.makeSeq multitac [] atomicMultitac)
  | multitac SEMI (multitac)
  | atomicMultitac %prec SEMI (atomicMultitac)

multitac : rawMultitac (annotate (Pos.pos (rawMultitac1left fileName) (rawMultitac1right fileName)) rawMultitac)

hypBinding
  : boundVar COLON sort (Option.getOpt (boundVar, "_"), sort)
  | boundVar (Option.getOpt (boundVar, "_"), O.EXP)

hypBindings
  : hypBinding hypBindings %prec COMMA (hypBinding :: hypBindings)
  | ([])

rawTactic
  : multitac %prec SEMI (Tac.multitacToTac multitac)

tactic
  : rawTactic (annotate (Pos.pos (rawTactic1left fileName) (rawTactic1right fileName)) rawTactic)

tactics
  : tactic ([tactic])
  | tactic COMMA tactics (tactic :: tactics)
  | tactic COMMA ([tactic])

declArgument
  : metavar COLON valence ((metavar, valence))
  | metavar ((metavar, ([], O.EXP)))

declArguments
  : declArgument ([declArgument])
  | declArgument COMMA declArguments (declArgument :: declArguments)
  | ([])

declArgumentsParens
  : LPAREN declArguments RPAREN (declArguments)
  | ([])

decl
  : DCL_DEF OPNAME declArgumentsParens COLON sort EQUALS LSQUARE term RSQUARE
      (MlId.const OPNAME, Signature.Src.DEF {arguments = declArgumentsParens, definiens = term, sort = sort}, Pos.pos (OPNAMEleft fileName) (OPNAMEright fileName))
  | DCL_DEF OPNAME declArgumentsParens EQUALS LSQUARE term RSQUARE
      (MlId.const OPNAME, Signature.Src.DEF {arguments = declArgumentsParens, definiens = term, sort = O.EXP}, Pos.pos (OPNAMEleft fileName) (OPNAMEright fileName))
  | DCL_TAC OPNAME declArgumentsParens EQUALS LSQUARE tactic RSQUARE
      (MlId.const OPNAME, Signature.Src.TAC {arguments = declArgumentsParens, script = tactic}, Pos.pos (OPNAMEleft fileName) (OPNAMEright fileName))
  | DCL_THM OPNAME declArgumentsParens COLON LSQUARE src_atjdg RSQUARE BY LSQUARE tactic RSQUARE
      (MlId.const OPNAME, Signature.Src.THM {arguments = declArgumentsParens, goal = src_atjdg, script = tactic}, Pos.pos (OPNAMEleft fileName) (OPNAMEright fileName))

rawCmd
  : CMD_PRINT OPNAME (Signature.Src.PRINT (MlId.const OPNAME))
  | CMD_EXTRACT OPNAME (Signature.Src.EXTRACT (MlId.const OPNAME))
  | CMD_QUIT (Signature.Src.QUIT)

cmd : rawCmd (rawCmd, (Pos.pos (rawCmd1left fileName) (rawCmd1right fileName)))

elt
  : cmd (Signature.Src.CMD cmd)
  | decl (Signature.Src.DECL decl)

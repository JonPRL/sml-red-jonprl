structure Ast = RedPrlAst
structure Ar = RedPrlArity
structure Vl = Ar.Vl

open Ast

structure P =
struct
  open RedPrlParamData
  open RedPrlParameterTerm
end

structure O = RedPrlOpData

fun makeFunApp (m, n) =
  Ast.$$ (O.MONO O.AP, [\ (([],[]), m), \ (([],[]), n)])

fun makeFunApps [x] = x
  | makeFunApps (x :: xs) = makeFunApp (makeFunApps xs, x)
  | makeFunApps [] = raise Fail "makeFunApps"

%%
%header (functor RedPrlLrValsFun (structure Token : TOKEN))

%term
   EOF
 | IDENT of string
 | COLON
 | LPAREN
 | RPAREN
 | RBRACKET
 | LBRACKET
 | LSQUARE
 | RSQUARE
 | LTEQUALS
 | DOT
 | APOSTROPHE
 | ZERO
 | ONE
 | HASH
 | SEMI
 | COMMA
 | LAMBDA
 | SQUIGGLE_ARROW
 | HCOM
 | COE

 | DIM | EXN | LBL
 | EXP | LVL

 | DEF

%nonterm
   start of ast
 | term of ast
 | preterm of ast
 | binder of symbol list * symbol list
 | binding of ast abs
 | bindings of ast abs list
 | symbols of symbol list
 | param of string param
 | params of string param list
 | extents of string param list
 | terms of ast list
 | metavar of string
 | metavarArgs of string param list * ast list
 | operator of string operator
 | funapp of ast list
 | span of string param * string param
 | psort of P.param_sort
 | sort of O.sort
 | psorts of P.param_sort list
 | sorts of O.sort list
 | binding_sig of P.param_sort list * O.sort list
 | valence of Ar.valence

%verbose
%pos Coord.t
%start start
%eop EOF
%noshift EOF
%name RedPrl
%arg (fileName) : string
%%

start : term (term)

psort
  : DIM (P.DIM)
  | EXN (P.EXN)
  | LBL (P.LBL)

sort
  : EXP (O.EXP)
  | LVL (O.LVL)

sorts
  : ([])
  | sort ([sort])
  | sort COMMA sorts (sort :: sorts)

psorts
  : ([])
  | psort ([psort])
  | psort COMMA psorts (psort :: psorts)

binding_sig
  : LBRACKET psorts RBRACKET LSQUARE sorts RSQUARE DOT (psorts, sorts)
  | LBRACKET psorts RBRACKET DOT (psorts, [])
  | LSQUARE sorts RSQUARE DOT ([], sorts)
  | ([], [])

valence
  : binding_sig sort ((binding_sig, sort))

param
  : IDENT (P.VAR IDENT)
  | ZERO (P.APP P.DIM0)
  | ONE (P.APP P.DIM1)

params
  : ([])
  | param ([param])
  | param COMMA params (param :: params)

extents
  : param ([param])
  | param COMMA extents (param :: extents)

symbols
  : ([])
  | IDENT ([IDENT])
  | IDENT COMMA symbols (IDENT :: symbols)

terms
  : ([])
  | term ([term])
  | term COMMA terms (term :: terms)

binder
  : LBRACKET symbols RBRACKET LSQUARE symbols RSQUARE DOT (symbols1, symbols2)
  | LBRACKET symbols RBRACKET DOT (symbols, [])
  | LSQUARE symbols RSQUARE DOT ([], symbols)
  | ([], [])

binding
  : binder term (\ (binder, term))

bindings
  : ([])
  | binding ([binding])
  | binding SEMI bindings (binding :: bindings)

metavarArgs
  : LBRACKET params RBRACKET LSQUARE terms RSQUARE (params, terms)
  | LBRACKET params RBRACKET (params, [])
  | LSQUARE terms RSQUARE ([], terms)
  | ([], [])

operator
  : LAMBDA (O.MONO O.LAM)
  | HCOM LSQUARE extents SEMI span RSQUARE (O.POLY (O.HCOM (O.TAG_NONE, extents, span)))
  | COE LSQUARE span RSQUARE (O.POLY (O.COE (O.TAG_NONE, span)))
  | IDENT (O.POLY (O.CUST (IDENT, NONE)))

metavar
  : HASH IDENT (IDENT)

funapp
  : term ([term])
  | term funapp (funapp @ [term])

span
  : param SQUIGGLE_ARROW param ((param1, param2))

term : preterm (annotate (Pos.pos preterm1left preterm1right) preterm)

preterm
  : operator LPAREN bindings RPAREN (Ast.$$ (operator, bindings))
  | metavar metavarArgs (Ast.$$# (metavar, metavarArgs))
  | IDENT (`` IDENT)
  | funapp (makeFunApps funapp)
  | LPAREN term RPAREN (term)
